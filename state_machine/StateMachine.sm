%class StateMachine
%header rm_fsm/StateMachine.h

%include <rm_msgs/DbusData.h>
%include <rm_msgs/TofRadarData.h>
%include <rm_msgs/GameRobotStatus.h>
%import rm_msgs

%declare #include <rm_msgs/DbusData.h>
%declare #include <rm_msgs/TofRadarData.h>
%declare #include <rm_msgs/GameRobotStatus.h>

%declare using namespace rm_msgs;

%start StateMachineMap::Stop

%map StateMachineMap
%%

// Syntax:
//
// State {
//  TransitionEvent
//      [guard]
//      NextState
//      {Action}
// }

Stop
    Entry {
    info("Stop state entered");
    controller_manager_.stopMainControllers();
    controller_manager_.stopCalibrationControllers();
    }
{
    dbusUpdate(data: const DbusData &)
        [ data.s_r == DbusData::DOWN ]
        Idle
        {
        }
}

Idle
    Entry {
    info("Idle state entered");
    controller_manager_.startMainControllers();
    calibrateChassisGimbal();
    calibrateShooter();
    }
{
    dbusUpdate(data: const DbusData &)
        [ data.s_r == DbusData::MID ]
        Manual
        {
        }
}

Manual
    Entry {
    info("Raw state entered");
    }
{
    dbusUpdate(data: const DbusData &)
        [ data.s_r == DbusData::DOWN ]
        Idle
        {
        }
    dbusUpdate(data: const DbusData &)
        [ data.s_r == DbusData::UP ]
        Cruise
        {
        }
    dbusUpdate(data: const DbusData &)
        nil
        {
            sendChassisCmd(false, data);
            sendGimbalCmd(false, data, ctxt.lower_cmd_sender_);
            sendShooterCmd(false, data, ctxt.lower_cmd_sender_);
        }
}

Cruise
    Entry {
            info("Cruise state entered");
    }
{
    dbusUpdate(data: const DbusData &)
        [ data.s_r == DbusData::MID ]
        Manual
        {
        }
    dbusUpdate(data: const DbusData &)
        nil
        {
            reversal(ctxt.enable_random_reversal_);
            sendChassisCmd(true, data);
            sendGimbalCmd(true, data, ctxt.lower_cmd_sender_);
            sendShooterCmd(true, data, ctxt.lower_cmd_sender_);
        }
    leftRadarCB(data: const TofRadarData &)
        [ data.distance < ctxt.safety_distance_ && ctxt.auto_linear_vel_ > 0 ]
        nil
        {
            catapult();
        }
    rightRadarCB(data: const TofRadarData &)
        [ data.distance < ctxt.safety_distance_ && ctxt.auto_linear_vel_ < 0 ]
        nil
        {
            catapult();
        }
}

Default
{
    robotStatusUpdate(data: const rm_msgs::GameRobotStatus &)
        [ (data.mains_power_chassis_output == 1 && ctxt.subscriber_.referee_.game_robot_status_.mains_power_chassis_output_== 0) ||
          (data.mains_power_gimbal_output == 1 && ctxt.subscriber_.referee_.game_robot_status_.mains_power_gimbal_output_== 0) ]
        nil
        {
            info("Chassis and gimbal power on");
        }
    robotStatusUpdate(data: const rm_msgs::GameRobotStatus &)
        [ data.mains_power_shooter_output == 1 && ctxt.subscriber_.referee_.game_robot_status_.mains_power_shooter_output_== 0 ]
        nil
        {
            info("Shooter power on");
        }
    robotStatusUpdate(data: const rm_msgs::GameRobotStatus &)
        nil
        {
        }
    dbusUpdate(data: const DbusData &)
        nil
        {
        }

    leftRadarCB(data: const TofRadarData &)
        nil
        {
        }
    rightRadarCB(data: const TofRadarData &)
        nil
        {
        }
    checkRc()
        [ ros::Time::now() - ctxt.subscriber_.dbus_.stamp > ros::Duration(0.1) ]
        Stop
        {
            info("Remote controller timeout");
        }
    checkRc()
        nil
        {
        }
}
%%
